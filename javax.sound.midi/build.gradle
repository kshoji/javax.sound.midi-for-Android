plugins {
    id 'com.android.library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    api 'com.android.support:support-annotations:28.0.0'
}

android {
    defaultConfig {
        compileSdk 34
        minSdkVersion 12
        targetSdkVersion 34
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        //instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    defaultConfig {
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    namespace 'jp.kshoji.javax.sound.midi'

    buildFeatures {
        renderScript true
        aidl true
    }
}

publishing {
    publications {
        release(MavenPublication) {
            group = 'jp.kshoji'
            artifactId = 'javax-sound-midi'
            version = '0.0.5'

            afterEvaluate {
                from components.release
            }
        }
    }

    repositories {
        maven {
            url = "${project.projectDir}/repository"
        }
    }
}
